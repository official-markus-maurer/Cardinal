#version 450
layout (local_size_x = 16, local_size_y = 16) in;

layout (set = 0, binding = 0) uniform sampler2D inputTexture;
layout (set = 0, binding = 1, rgba16f) uniform writeonly image2D outputImage;

layout (push_constant) uniform PushConstants {
    vec4 params; // x: threshold, y: knee, z: unused, w: unused
} pc;

void main() {
    ivec2 uv = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(outputImage);
    if (uv.x >= size.x || uv.y >= size.y) return;

    vec2 texCoord = (vec2(uv) + 0.5) / vec2(size);

    // Downsample with linear filtering
    vec3 color = texture(inputTexture, texCoord).rgb;

    // Thresholding (Quadratic threshold)
    float threshold = pc.params.x;
    float knee = pc.params.y;
    
    float brightness = max(color.r, max(color.g, color.b));
    float soft = brightness - threshold + knee;
    soft = clamp(soft, 0.0, 2.0 * knee);
    soft = soft * soft / (4.0 * knee + 0.00001);
    float contribution = max(soft, brightness - threshold);
    contribution /= max(brightness, 0.00001);
    
    vec3 bloom = color * contribution;

    imageStore(outputImage, uv, vec4(bloom, 1.0));
}
