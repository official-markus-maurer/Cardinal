#version 450
layout(local_size_x = 16, local_size_y = 16) in;
layout(binding = 0, r8) writeonly uniform image2D outBlur;
layout(binding = 1) uniform sampler2D ssaoInput;

void main() {
    ivec2 pixelPos = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(outBlur);
    if (pixelPos.x >= size.x || pixelPos.y >= size.y) return;
    
    vec2 texelSize = 1.0 / vec2(size);
    float result = 0.0;
    
    // 4x4 blur
    for (int x = -2; x < 2; ++x) {
        for (int y = -2; y < 2; ++y) {
            vec2 offset = vec2(float(x), float(y)) * texelSize;
            result += texture(ssaoInput, (vec2(pixelPos) + 0.5) * texelSize + offset).r;
        }
    }
    
    imageStore(outBlur, pixelPos, vec4(vec3(result / 16.0), 1.0));
}
